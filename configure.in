dnl Process this file with autoconf to produce a configure script.
dnl You may need the --localdir option to get the included .m4's
dnl
dnl Maketool - GTK-based front end for gmake
dnl Copyright (c) 1999-2001 Greg Banks
dnl 
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl 
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl 
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
dnl
dnl $Id: configure.in,v 1.35 2001-09-21 05:15:03 gnb Exp $
dnl

AC_INIT(main.c)
sinclude(/usr/share/aclocal/gettext.m4)dnl
sinclude(/usr/share/aclocal/lcmessage.m4)dnl
sinclude(/usr/share/aclocal/progtest.m4)dnl
sinclude(/usr/share/aclocal/gtk.m4)dnl
AC_CONFIG_HEADER(config.h)

PACKAGE=maketool
VERSION=0.6.4

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_AWK
AC_PROG_CC
AC_ISC_POSIX

dnl Get name of GNU make
AC_ARG_WITH(gmake,
[  --with-gmake=CMD        Set the GNU make command (may include full
                          pathname and flags)],
[case "$withval" in
no) AC_MSG_ERROR(Maketool requires gmake) ;;
yes) AC_MSG_ERROR(Please specify a gmake command or let configure figure it out) ;;
*) GMAKE="$withval" ;;
esac])
if test -z "$GMAKE"; then
AC_CHECK_PROGS(GMAKE, gmake make, make)
fi

dnl Add filters for MWOS cross-compiler. This
dnl defaults to off 'cos it's fairly unusual.
AC_MSG_CHECKING([whether MWOS cross-compiler support is requested])
AC_ARG_ENABLE(mwos,
[  --enable-mwos           Enable filters for MWOS cross-compiler],
ENABLE_MWOS=$enableval,ENABLE_MWOS=no)
AC_MSG_RESULT($ENABLE_MWOS)
if test "$ENABLE_MWOS" = "yes"; then
    AC_DEFINE(ENABLE_MWOS)
fi

dnl Test for printer configuration.
dnl File /etc/printcap (in BSD-derived systems)
PRINTCAP=/etc/printcap
AC_MSG_CHECKING(for presence of $PRINTCAP)
if test -f $PRINTCAP ; then
  AC_DEFINE(HAVE_ETC_PRINTCAP,1)
  AC_MSG_RESULT(yes)
else
  AC_DEFINE(HAVE_ETC_PRINTCAP,0)
  AC_MSG_RESULT(no)
fi

dnl Program lpstat (SysV-derived systems)
AC_PATH_PROG(LPSTAT_COMMAND, lpstat)
AC_DEFINE_UNQUOTED(LPSTAT_COMMAND, "$LPSTAT_COMMAND")
if test -z "$LPSTAT_COMMAND" ; then
  AC_DEFINE(HAVE_LPSTAT,0)
else
  AC_DEFINE(HAVE_LPSTAT,1)
fi


dnl Stuff for gettext
ALL_LINGUAS="ja cs"
AM_GNU_GETTEXT

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(malloc.h sys/ioctl.h sys/time.h unistd.h memory.h signal.h sys/filio.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
AC_FUNC_WAIT3
AC_CHECK_FUNCS(putenv regcomp strchr)


dnl ===============================
dnl Test for BSD vs SysV signal semantics
AC_MSG_CHECKING(for signal semantics)
AC_TRY_RUN([
#include <signal.h>

static int ncaught;

static RETSIGTYPE
handler(int sig)
{
    ncaught++;
}

int
main(int argc, char **argv)
{
    int i;
    

#ifdef SIGCHLD
    signal(SIGCHLD, handler);
#else
    signal(SIGCLD, handler);
#endif
    for (i=0 ; i<5 ; i++)
	kill(getpid(), SIGCLD);

    switch (ncaught)
    {
    case 1: /* SysV signal semantics */
    	return 0;
    case 5: /* BSD signal semantics */
    	return 1;
    default: /* something is awry */
    	return 2;
    }
}
],AC_DEFINE(HAVE_SYSV_SIGNAL,1) AC_MSG_RESULT(sysv),
AC_DEFINE(HAVE_SYSV_SIGNAL,0) AC_MSG_RESULT(bsd),
AC_DEFINE(HAVE_SYSV_SIGNAL,0) AC_MSG_RESULT(assuming bsd))
dnl ===============================


dnl GTK
AM_PATH_GTK(1.2.0)

dnl **HACK** temporarily configure out online help cos its immature
AC_MSG_CHECKING([whether immature online help is requested])
AC_ARG_ENABLE(online-help,
[  --enable-online-help    Enable immature online help (developers only)],
ENABLE_ONLINE_HELP=$enableval,ENABLE_ONLINE_HELP=no)
AC_MSG_RESULT($ENABLE_ONLINE_HELP)
if test "$ENABLE_ONLINE_HELP" = "yes"; then
    AC_DEFINE(ENABLE_ONLINE_HELP)
fi
AC_SUBST(ENABLE_ONLINE_HELP)



AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE_UNQUOTED(GMAKE, "$GMAKE")
AC_DEFINE(HAVE_WAIT3)
AC_SUBST(ALL_LINGUAS)
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_OUTPUT(Makefile extract_targets maketool.spec intl/Makefile po/Makefile.in makesys/Makefile icons/Makefile doc/Makefile doc/help/Makefile doc/help/help-common.mk)
