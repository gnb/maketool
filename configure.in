dnl Process this file with autoconf to produce a configure script.
dnl You may need the --localdir option to get the included .m4's
dnl
dnl Maketool - GTK-based front end for gmake
dnl Copyright (c) 1999-2000 Greg Banks
dnl 
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl 
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl 
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
dnl
dnl $Id: configure.in,v 1.24 2000-08-01 15:37:29 gnb Exp $
dnl

AC_INIT(main.c)
sinclude(/usr/share/aclocal/gettext.m4)dnl
sinclude(/usr/share/aclocal/lcmessage.m4)dnl
sinclude(/usr/share/aclocal/progtest.m4)dnl
sinclude(/usr/share/aclocal/gtk.m4)dnl
AC_CONFIG_HEADER(config.h)

PACKAGE=maketool
VERSION=0.6.1

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_AWK
AC_PROG_CC

dnl Get name of GNU make
AC_ARG_WITH(gmake,
[  --with-gmake=CMD        Set the GNU make command (may include full
                          pathname and flags)],
[case "$withval" in
no) AC_MSG_ERROR(Maketool requires gmake) ;;
yes) AC_MSG_ERROR(Please specify a gmake command or let configure figure it out) ;;
*) GMAKE="$withval" ;;
esac])
if test -z "$GMAKE"; then
AC_CHECK_PROGS(GMAKE, gmake make, make)
fi

dnl Add filters for MWOS cross-compiler. This
dnl defaults to off 'cos it's fairly unusual.
AC_MSG_CHECKING([whether MWOS cross-compiler support is requested])
AC_ARG_ENABLE(mwos,
[  --enable-mwos           Enable filters for MWOS cross-compiler],
ENABLE_MWOS=$enableval,ENABLE_MWOS=no)
AC_MSG_RESULT($ENABLE_MWOS)
if test "$ENABLE_MWOS" = "yes"; then
    AC_DEFINE(ENABLE_MWOS)
fi

dnl Test for existance of /usr/doc/ directory (e.g. on RedHat Linux)
dnl Note we don't test for $prefix/doc because that may not exist
dnl yet if we are building an RPM.
DOCDIR=/usr/doc
AC_MSG_CHECKING(for presence of $DOCDIR)
changequote(,)dnl
if test -d $DOCDIR ; then
  USRDOC=
  AC_MSG_RESULT(yes)
else
  USRDOC="#USRDOC#"
  AC_MSG_RESULT(no)
fi
changequote([,])dnl
AC_SUBST(USRDOC)


dnl Test for printer configuration.
dnl File /etc/printcap (in BSD-derived systems)
PRINTCAP=/etc/printcap
AC_MSG_CHECKING(for presence of $PRINTCAP)
if test -f $PRINTCAP ; then
  AC_DEFINE(HAVE_ETC_PRINTCAP,1)
  AC_MSG_RESULT(yes)
else
  AC_DEFINE(HAVE_ETC_PRINTCAP,0)
  AC_MSG_RESULT(no)
fi

dnl Program lpstat (SysV-derived systems)
AC_PATH_PROG(LPSTAT_COMMAND, lpstat)
AC_DEFINE_UNQUOTED(LPSTAT_COMMAND, "$LPSTAT_COMMAND")
if test -z "$LPSTAT_COMMAND" ; then
  AC_DEFINE(HAVE_LPSTAT,0)
else
  AC_DEFINE(HAVE_LPSTAT,1)
fi


dnl Stuff for gettext
ALL_LINGUAS="la"
AM_GNU_GETTEXT

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(malloc.h sys/ioctl.h sys/time.h unistd.h memory.h signal.h sys/filio.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
AC_FUNC_WAIT3
AC_CHECK_FUNCS(putenv regcomp strchr)

dnl GTK
AM_PATH_GTK(1.2.0)

AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE_UNQUOTED(GMAKE, "$GMAKE")
AC_DEFINE(HAVE_WAIT3)
AC_SUBST(ALL_LINGUAS)
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_OUTPUT(Makefile extract_targets make_makefile maketool.spec intl/Makefile po/Makefile.in icons/Makefile doc/help/Makefile)
